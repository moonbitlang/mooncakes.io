<!DOCTYPE html>
<html lang="en">

<head>
  <meta charset="UTF-8">
  <title>Moonbit docs</title>
  <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
  <meta name="description" content="Description">
  <meta name="viewport" content="width=device-width, initial-scale=1.0, minimum-scale=1.0">
  <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/styles/base16/one-light.css">
  <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/highlight.min.js"></script>
  <script
    src="//cdnjs.cloudflare.com/ajax/libs/highlightjs-line-numbers.js/2.8.0/highlightjs-line-numbers.min.js"></script>
</head>

<body>
  <pre><code class="moonbit">//double erf(double x)
//double erfc(double x)
//		     x
//	      2      |\
//    erf(x)  =  ---------  | exp(-t*t)dt
// 	   sqrt(pi) \| 
//		     0
//
//    erfc(x) =  1-erf(x)
// Note that 
//	erf(-x) = -erf(x)
//	erfc(-x) = 2 - erfc(x)
//
//Method:
//1. For |x| in [0, 0.84375]
//    erf(x)  = x + x*R(x^2)
//         erfc(x) = 1 - erf(x)           if x in [-.84375,0.25]
//                 = 0.5 + ((0.5-x)-x*R)  if x in [0.25,0.84375]
//   where R = P/Q where P is an odd poly of degree 8 and
//   Q is an odd poly of degree 10.
//					 -57.90
//		| R - (erf(x)-x)/x | <= 2
//
//
//   Remark. The formula is derived by noting
//         erf(x) = (2/sqrt(pi))*(x - x^3/3 + x^5/10 - x^7/42 + ....)
//   and that
//         2/sqrt(pi) = 1.128379167095512573896158903121545171688
//   is close to one. The interval is chosen because the fix
//   point of erf(x) is near 0.6174 (i.e., erf(x)=x when x is
//   near 0.6174), and by some experiment, 0.84375 is chosen to
//	   guarantee the error is less than one ulp for erf.
//
//     2. For |x| in [0.84375,1.25], let s = |x| - 1, and
//        c = 0.84506291151 rounded to single (24 bits)
//        	erf(x)  = sign(x) * (c  + P1(s)/Q1(s))
//        	erfc(x) = (1-c)  - P1(s)/Q1(s) if x > 0
//		  1+(c+P1(s)/Q1(s))    if x < 0
//        	|P1/Q1 - (erf(|x|)-c)| <= 2**-59.06
//   Remark: here we use the taylor series expansion at x=1.
//	erf(1+s) = erf(1) + s*Poly(s)
//		 = 0.845.. + P1(s)/Q1(s)
//   That is, we use rational approximation to approximate
//		erf(1+s) - (c = (single)0.84506291151)
//   Note that |P1/Q1|< 0.078 for x in [0.84375,1.25]
//   where 
//	P1(s) = degree 6 poly in s
//	Q1(s) = degree 6 poly in s
//
//     3. For x in [1.25,1/0.35(~2.857143)], 
//        	erfc(x) = (1/x)*exp(-x*x-0.5625+R1/S1)
//        	erf(x)  = 1 - erfc(x)
//   where 
//	R1(z) = degree 7 poly in z, (z=1/x^2)
//	S1(z) = degree 8 poly in z
//
//     4. For x in [1/0.35,28]
//        	erfc(x) = (1/x)*exp(-x*x-0.5625+R2/S2) if x > 0
//		= 2.0 - (1/x)*exp(-x*x-0.5625+R2/S2) if -6<x<0
//		= 2.0 - tiny		(if x <= -6)
//        	erf(x)  = sign(x)*(1.0 - erfc(x)) if x < 6, else
//        	erf(x)  = sign(x)*(1.0 - tiny)
//   where
//	R2(z) = degree 6 poly in z, (z=1/x^2)
//	S2(z) = degree 7 poly in z
//
//     Note1:
//   To compute exp(-x*x-0.5625+R/S), let s be a single
//   precision number and s := x; then
//	-x*x = -s*s + (s-x)*(s+x)
//        exp(-x*x-0.5626+R/S) = 
//		exp(-s*s-0.5625)*exp((s-x)*(s+x)+R/S);
//     Note2:
//   Here 4 and 5 make use of the asymptotic series
//		  exp(-x*x)
//	erfc(x) ~ ---------- * ( 1 + Poly(1/x^2) )
//		  x*sqrt(pi)
//   We use rational approximation to approximate
//     	g(s)=f(1/x^2) = log(erfc(x)*x) - x*x + 0.5625
//   Here is the error bound for R1/S1 and R2/S2
//     	|R1/S1 - f(x)|  < 2**(-62.57)
//     	|R2/S2 - f(x)|  < 2**(-61.52)
//
//     5. For inf > x >= 28
//        	erf(x)  = sign(x) *(1 - tiny)  (raise inexact)
//        	erfc(x) = tiny*tiny (raise underflow) if x > 0
//		= 2 - tiny if x<0
//
//     7. Special case:
//        	erf(0)  = 0, erf(inf)  = 1, erf(-inf) = -1,
//        	erfc(0) = 1, erfc(inf) = 0, erfc(-inf) = 2, 
//   	erfc/erf(NaN) is NaN
//
///| Compute the error function of `x`.
/// # Special Cases
///
/// 1. erf(0) = 0
/// 2. erf(inf) = 1
/// 3. erf(-inf) = -1
/// 4. erf(NaN) = NaN
pub fn erf(x: Double) -> Double {
  if isnan(x) {
    return double_nan
  }

  if isinf(x) {
    return if x > 0 { 1.0 } else { -1.0 }
  }

  let efx8 = 1.02703333676410069053
  let efx = 1.28379167095512586316
  let tiny = 1.0e-300
  let erx = 8.45062911510467529297e-01
  let pp0 = 1.28379167095512558561e-01
  let pp1 = -3.25042107247001499370e-01
  let pp2 = -2.84817495755985104766e-02
  let pp3 = -5.77027029648944159157e-03
  let pp4 = -2.37630166566501626084e-05
  let qq1 = 3.97917223959155352819e-01
  let qq2 = 6.50222499887672944485e-02
  let qq3 = 5.08130628187576562776e-03
  let qq4 = 1.32494738004321644526e-04
  let qq5 = -3.96022827877536812320e-06

  let pa0 = -2.36211856075265944077e-03
  let pa1 = 4.14856118683748331666e-01
  let pa2 = -3.72207876035701323847e-01
  let pa3 = 3.18346619901161753674e-01
  let pa4 = -1.10894694282396677476e-01
  let pa5 = 3.54783043256182359371e-02
  let pa6 = -2.16637559486879084300e-03
  let qa1 = 1.06420880400844228286e-01
  let qa2 = 5.40397917702171048937e-01
  let qa3 = 7.18286544141962662868e-02
  let qa4 = 1.26171219808761642112e-01
  let qa5 = 1.36370839120290507362e-02
  let qa6 = 1.19844998467991074170e-02
  let ra0 = -9.86494403484714822705e-03
  let ra1 = -6.93858572707181764372e-01
  let ra2 = -1.05586262253232909814e+01
  let ra3 = -6.23753324503260060396e+01
  let ra4 = -1.62396669462573470355e+02
  let ra5 = -1.84605092906711035994e+02
  let ra6 = -8.12874355063065934246e+01
  let ra7 = -9.81432934416914548592e+00
  let sa1 = 1.96512716674392571292e+01
  let sa2 = 1.37657754143519042600e+02
  let sa3 = 4.34565877475229228821e+02
  let sa4 = 6.45387271733267880336e+02
  let sa5 = 4.29008140027567833386e+02
  let sa6 = 1.08635005541779435134e+02
  let sa7 = 6.57024977031928170135e+00
  let sa8 = -6.04244152148580987438e-02

  let rb0 = -9.86494292470009928597e-03
  let rb1 = -7.99283237680523006574e-01
  let rb2 = -1.77579549177547519889e+01
  let rb3 = -1.60636384855821916062e+02
  let rb4 = -6.37566443368389627722e+02
  let rb5 = -1.02509513161107724954e+03
  let rb6 = -4.83519191608651397019e+02
  let sb1 = 3.03380607434824582924e+01
  let sb2 = 3.25792512996573918826e+02
  let sb3 = 1.53672958608443695994e+03
  let sb4 = 3.19985821950859553908e+03
  let sb5 = 2.55305040643316442583e+03
  let sb6 = 4.74528541206955367215e+02
  let sb7 = -2.24409524465858183362e+01

  let hx = __hi(x).reinterpret_as_int()
  let ix = hx & 0x7fffffff

  if ix < 0x3feb0000 {
    if ix < 0x3e300000 {
      if ix < 0x00800000 {
        return 0.125 * (8.0 * x + efx8 * x)
      }
      return x + efx * x
    }
    let z = x * x
    let r = pp0 + z * (pp1 + z * (pp2 + z * (pp3 + z * pp4)))
    let s = 1.0 + z * (qq1 + z * (qq2 + z * (qq3 + z * (qq4 + z * qq5))))
    let y = r / s
    return x + x * y
  }

  if ix < 0x3ff40000 {
    let s = fabs(x) - 1.0
    let p = pa0 + s * (pa1 + s * (pa2 + s * (pa3 + s * (pa4 + s * (pa5 + s * pa6)))))
    let q = 1.0 + s * (qa1 + s * (qa2 + s * (qa3 + s * (qa4 + s * (qa5 + s * qa6)))))
    return if hx >= 0 { erx + p / q } else { -erx - p / q }
  }

  if ix >= 0x40180000 {
    return if hx >= 0 { 1.0 - tiny } else { tiny - 1.0 }
  }

  let x = fabs(x)
  let s = 1.0 / (x * x)

  let (r, s) = if ix < 0x4006DB6E {
    let r = ra0 + s * (ra1 + s * (ra2 + s * (ra3 + s * (ra4 + s * (ra5 + s * (ra6 + s * ra7))))))
    let s = 1.0 + s * (sa1 + s * (sa2 + s * (sa3 + s * (sa4 + s * (sa5 + s * (sa6 + s * (sa7 + s * sa8)))))))
    (r, s)
  } else {
    let r = rb0 + s * (rb1 + s * (rb2 + s * (rb3 + s * (rb4 + s * (rb5 + s * rb6)))))
    let s = 1.0 + s * (sb1 + s * (sb2 + s * (sb3 + s * (sb4 + s * (sb5 + s * (sb6 + s * sb7))))))
    (r, s)
  }

  let mut z = x
  z = __combineW(__hi(z), 0)
  let r = exp(-z * z - 0.5625) * exp((z - x) * (z + x) + r / s)
  if hx >= 0 { 1.0 - r / x } else { r / x - 1.0 }
}

test "erf" {
  fn assert_erf_ulp!(input, expect) {
    assert_ulp!(expect, erf(input), ERF_MAX_ULP);
  }
  assert_erf_ulp!(-0.8, -0.7421009647076605)
  assert_erf_ulp!(-0.7, -0.6778011938374184)
  assert_erf_ulp!(-0.6, -0.6038560908479259)
  assert_erf_ulp!(-0.5, -0.5204998778130465)
  assert_erf_ulp!(-0.4, -0.42839235504666845)
  assert_erf_ulp!(-0.3, -0.3286267594591274)
  assert_erf_ulp!(-0.2, -0.22270258921047847)
  assert_erf_ulp!(-0.1, -0.1124629160182849)
  assert_erf_ulp!(-0, -0)
  assert_erf_ulp!(-3.141592653589793, -0.9999911238536323)
  assert_erf_ulp!(-1.5707963267948966, -0.9736789250782586)
  assert_erf_ulp!(-0.7853981633974483, -0.7333114255659121)
  assert_erf_ulp!(0, 0)
  assert_erf_ulp!(0.1, 0.1124629160182849)
  assert_erf_ulp!(0.2, 0.22270258921047847)
  assert_erf_ulp!(0.3, 0.3286267594591274)
  assert_erf_ulp!(0.4, 0.42839235504666845)
  assert_erf_ulp!(0.5, 0.5204998778130465)
  assert_erf_ulp!(0.6, 0.6038560908479259)
  assert_erf_ulp!(0.7, 0.6778011938374184)
  assert_erf_ulp!(0.8, 0.7421009647076605)
  assert_erf_ulp!(0.9, 0.7969082124228322)
  assert_erf_ulp!(1, 0.8427007929497149)
  assert_erf_ulp!(3.141592653589793, 0.9999911238536323)
  assert_erf_ulp!(1.5707963267948966, 0.9736789250782586)
  assert_erf_ulp!(0.7853981633974483, 0.7333114255659121)
  assert_erf_ulp!(-1, -0.8427007929497149)
  assert_erf_ulp!(-2, -0.9953222650189527)
  assert_erf_ulp!(-3, -0.9999779095030014)
  assert_erf_ulp!(-4, -0.9999999845827421)
  assert_erf_ulp!(-5, -0.9999999999984626)
  assert_erf_ulp!(-6, -1)
  assert_erf_ulp!(-7, -1)
  assert_erf_ulp!(-8, -1)
  assert_erf_ulp!(-9, -1)
  assert_erf_ulp!(1, 0.8427007929497149)
  assert_erf_ulp!(2, 0.9953222650189527)
  assert_erf_ulp!(3, 0.9999779095030014)
  assert_erf_ulp!(4, 0.9999999845827421)
  assert_erf_ulp!(5, 0.9999999999984626)
  assert_erf_ulp!(6, 1)
  assert_erf_ulp!(7, 1)
  assert_erf_ulp!(8, 1)
  assert_erf_ulp!(9, 1)
  assert_erf_ulp!(10, 1)
  assert_erf_ulp!(100, 1)
  assert_erf_ulp!(1000, 1)
  assert_erf_ulp!(10000, 1)
  assert_erf_ulp!(2.5, 0.999593047982555)
  assert_erf_ulp!(3.4, 0.9999984780066371)
  assert_erf_ulp!(5.3, 0.9999999999999338)
  assert_erf_ulp!(6.2, 1)
  assert_erf_ulp!(7.1, 1)
  assert_erf_ulp!(8.9, 1)
  assert_erf_ulp!(9.8, 1)
  assert_erf_ulp!(10.7, 1)
  assert_erf_ulp!(101.6, 1)
  assert_erf_ulp!(1.542, 0.9707956683198804)
  assert_erf_ulp!(2.846, 0.9999429887922316)
  assert_erf_ulp!(7.881, 1)
  assert_erf_ulp!(3.772, 0.9999999041461628)
  assert_erf_ulp!(-1.542, -0.9707956683198804)
  assert_erf_ulp!(-2.846, -0.9999429887922316)
  assert_erf_ulp!(-7.881, -1)
  assert_erf_ulp!(-3.772, -0.9999999041461628)
  assert_erf_ulp!(-1, -0.8427007929497149)
  assert_erf_ulp!(0, 0)
  assert_erf_ulp!(-0, -0)
  assert_erf_ulp!(double_nan, double_nan)
  assert_erf_ulp!(double_pos_inf, 1)
  assert_erf_ulp!(double_neg_inf, -1)
}
</code></pre>
  <script>
    let moonbitLanguageFn = hljs => {
      return {
        case_insensitive: true,
        keywords: {
          keyword: 'func fn enum struct type if else match return continue break while let var interface pub priv readonly',
          literal: 'true false',
          type: "Int Int64 Double String Bool Char Bytes Option Array Result",
          built_in: 'lsl lsr asr shl shr land lor lxor Show Debug Hash Eq Compare Some None'
        },
        contains: [
          {
            scope: "char",
            begin: "'", end: "'"
          },
          {
            scope: "string",
            begin: "\"", end: "\""
          },
          {
            scope: "number",
            begin: "\\b\\d+(\\.\\d+)?\\b"
          },
          {
            scope: "codelink",
            match: /\<a href\="(?<link>[^<>]+?)"\>(?<code>[^\/<>]+?)\<\/a\>/g
          },
          hljs.COMMENT(
            '//', // begin
            '\n', // end
          )
        ]
      }
    }

    hljs.registerLanguage('moonbit', moonbitLanguageFn);
    hljs.highlightAll();
    hljs.initLineNumbersOnLoad();

    const number = window.location.href.split('#')[1];

    function waitForLineNumbers() {
      setTimeout(function () {
        const target = document.querySelector(`.hljs-ln-line[data-line-number="${number}"]`);
        if (target == null) waitForLineNumbers();
        else target.scrollIntoView();
      }, 50);
    }

    waitForLineNumbers()

  </script>
  <style>
    .hljs-ln-numbers {
      -webkit-touch-callout: none;
      -webkit-user-select: none;
      -khtml-user-select: none;
      -moz-user-select: none;
      -ms-user-select: none;
      user-select: none;
    }

    .hljs-ln-n {
      color: #ccc;
      border-right: 1px solid #dfdddd;
      margin-right: 1em;
      text-align: center;
      vertical-align: top;
      padding-right: 0.5em;
    }

    .hljs {
      background: none;
    }

    body {
      background-color: #fafafa;
    }
  </style>
</body>

</html>