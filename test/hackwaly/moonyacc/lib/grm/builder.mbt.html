<!DOCTYPE html>
<html lang="en">

<head>
  <meta charset="UTF-8">
  <title>Moonbit docs</title>
  <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
  <meta name="description" content="Description">
  <meta name="viewport" content="width=device-width, initial-scale=1.0, minimum-scale=1.0">
  <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/styles/base16/one-light.css">
  <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/highlight.min.js"></script>
  <script
    src="//cdnjs.cloudflare.com/ajax/libs/highlightjs-line-numbers.js/2.8.0/highlightjs-line-numbers.min.js"></script>
</head>

<body>
  <pre><code class="moonbit">///|
pub(all) typealias BuilderSymbol = String

///|
pub(readonly) struct BuilderContext {
  priv _set_prec : (BuilderSymbol, Associativity, Int) -> Unit
  priv _add_production : (BuilderSymbol, Array[BuilderSymbol]) -> Int
  priv _set_start : (BuilderSymbol) -> Unit
}

///|
pub fn BuilderContext::set_prec(
  self : BuilderContext,
  term : BuilderSymbol,
  assoc : Associativity,
  level : Int
) -> Unit {
  (self._set_prec)(term, assoc, level)
}

///|
pub fn BuilderContext::add_production(
  self : BuilderContext,
  lhs : BuilderSymbol,
  rhs : Array[BuilderSymbol]
) -> Int {
  (self._add_production)(lhs, rhs)
}

///|
pub fn BuilderContext::set_start(
  self : BuilderContext,
  name : BuilderSymbol
) -> Unit {
  (self._set_start)(name)
}

///|
pub fn build(f : (BuilderContext) -> Unit) -> Grammar {
  let terminal_by_name = @sorted_map.new()
  let nonterminal_by_name = @sorted_map.new()
  let terminals = []
  let nonterminals = []
  let productions = []
  fn get_terminal(name : BuilderSymbol) -> Terminal {
    match terminal_by_name[name] {
      Some(t) => t
      None => {
        let t = Terminal::{
          num: terminal_by_name.size(),
          name,
          references: [],
          prec: None,
        }
        terminal_by_name[name] = t
        terminals.push(t)
        t
      }
    }
  }

  fn get_nonterminal(name : BuilderSymbol) -> Nonterminal {
    match nonterminal_by_name[name] {
      Some(nt) => nt
      None => {
        let nt = Nonterminal::{
          num: nonterminal_by_name.size(),
          name,
          productions: [],
          references: [],
          nullability: NonNullable, // Transient
        }
        nonterminal_by_name[name] = nt
        nonterminals.push(nt)
        nt
      }
    }
  }

  let starts : Array[String] = []
  let mut num_productions = 0
  let mut production_added = false
  let defer = []
  let ctx = BuilderContext::{
    _add_production: fn(lhs, rhs) {
      let nt = get_nonterminal(lhs)
      let num = num_productions
      num_productions += 1
      defer.push(fn() {
        let mut last_prec = None
        let production = Production::{
          num,
          lhs: nt,
          rhs: rhs.map(fn(symbol) {
            match nonterminal_by_name[symbol] {
              Some(nt) => NT(nt)
              None => T(get_terminal(symbol))
            }
          }),
          prec: None,
        }
        productions.push(production)
        for index, symbol in production.rhs {
          match symbol {
            T(t) => {
              t.references.push((production, index))
              last_prec = t.prec.map(fn { (level, _) => level })
            }
            NT(nt) => nt.references.push((production, index))
          }
        }
        production.prec = last_prec
        nt.productions.push(production)
      })
      production_added = true
      num
    },
    _set_prec: fn(term, assoc, level) {
      guard not(production_added)
      let term = get_terminal(term)
      term.prec = Some((level, assoc))
    },
    _set_start: fn(name) { starts.push(name) },
  }
  f(ctx)
  for f in defer {
    f()
  }

  // Populate nullability
  for nt in nonterminals {
    if nt.productions.iter().all(fn(p) { p.rhs.is_empty() }) {
      nt.nullability = Null
    }
  }
  fn get_transient_non_nullables() {
    nonterminals.iter().filter(fn(nt) { nt.nullability == NonNullable })
  }

  @worklist.process(Iter::singleton(get_transient_non_nullables()), fn(
    nts,
    schedule
  ) {
    let mut changed = false
    fn is_nullable(symbol : Symbol) -> Bool {
      match symbol {
        T(_) => false
        NT({ nullability: Null | Nullable, .. }) => true
        NT({ nullability: NonNullable, .. }) => false
      }
    }

    for nt in nts {
      if nt.productions.iter().any(fn(p) { p.rhs.iter().all(is_nullable) }) {
        nt.nullability = Nullable
        changed = true
      }
    }
    if changed {
      schedule(get_transient_non_nullables())
    }
  })
  let starts = starts.map(fn(name) {
    let start_nt = get_nonterminal(name)
    guard start_nt.productions.length() == 1 &&
      start_nt.productions[0].rhs.length() == 1
    start_nt.productions[0]
  })
  { starts, terminals, nonterminals, productions }
}
</code></pre>
  <script>
    let moonbitLanguageFn = hljs => {
      return {
        case_insensitive: true,
        keywords: {
          keyword: 'func fn enum struct type if else match return continue break while let var interface pub priv readonly',
          literal: 'true false',
          type: "Int Int64 Double String Bool Char Bytes Option Array Result",
          built_in: 'lsl lsr asr shl shr land lor lxor Show Debug Hash Eq Compare Some None'
        },
        contains: [
          {
            scope: "char",
            begin: "'", end: "'"
          },
          {
            scope: "string",
            begin: "\"", end: "\""
          },
          {
            scope: "number",
            begin: "\\b\\d+(\\.\\d+)?\\b"
          },
          {
            scope: "codelink",
            match: /\<a href\="(?<link>[^<>]+?)"\>(?<code>[^\/<>]+?)\<\/a\>/g
          },
          hljs.COMMENT(
            '//', // begin
            '\n', // end
          )
        ]
      }
    }

    hljs.registerLanguage('moonbit', moonbitLanguageFn);
    hljs.highlightAll();
    hljs.initLineNumbersOnLoad();

    const number = window.location.href.split('#')[1];

    function waitForLineNumbers() {
      setTimeout(function () {
        const target = document.querySelector(`.hljs-ln-line[data-line-number="${number}"]`);
        if (target == null) waitForLineNumbers();
        else target.scrollIntoView();
      }, 50);
    }

    waitForLineNumbers()

  </script>
  <style>
    .hljs-ln-numbers {
      -webkit-touch-callout: none;
      -webkit-user-select: none;
      -khtml-user-select: none;
      -moz-user-select: none;
      -ms-user-select: none;
      user-select: none;
    }

    .hljs-ln-n {
      color: #ccc;
      border-right: 1px solid #dfdddd;
      margin-right: 1em;
      text-align: center;
      vertical-align: top;
      padding-right: 0.5em;
    }

    .hljs {
      background: none;
    }

    body {
      background-color: #fafafa;
    }
  </style>
</body>

</html>