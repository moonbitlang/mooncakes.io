<!DOCTYPE html>
<html lang="en">

<head>
  <meta charset="UTF-8">
  <title>Moonbit docs</title>
  <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
  <meta name="description" content="Description">
  <meta name="viewport" content="width=device-width, initial-scale=1.0, minimum-scale=1.0">
  <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/styles/base16/one-light.css">
  <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/highlight.min.js"></script>
  <script
    src="//cdnjs.cloudflare.com/ajax/libs/highlightjs-line-numbers.js/2.8.0/highlightjs-line-numbers.min.js"></script>
</head>

<body>
  <pre><code class="moonbit">///| Abstract syntax tree mappers.
///
/// Mappers help with pushing abstract syntax tree transformations in every
/// node with a minimal amount of code by defaulting the cases you
/// don't handle. The default map maps leaves to themselves and
/// otherwise propagates the map to all children.
///
/// This map has the form of `List::filter_map`, however it is akin
/// to `List.concat_map` as it allows:
/// - Node deletion by mapping to `None`
/// - Node transformation by mapping to `Some _`
/// - Node expansion by mapping to `Some (Inlines _)` or `Some (Blocks _)`
pub(all) struct Mapper {
  inline_ext_default : MapFn[Inline]
  block_ext_default : MapFn[Block]
  inline : MapperFn[Inline]
  block : MapperFn[Block]
}

///|
pub(all) typealias MapFn[A] = (Mapper, A) -> FilterMap[A]!MapperError

///|
pub(all) typealias MapperFn[A] = (Mapper, A) -> MapperResult[A]

///| The type for mapper results.
/// `Default` means doing the default map.
pub(all) enum MapperResult[A] {
  Default
  Map(FilterMap[A])
} derive(Eq, Show, ToJson)

///|
pub impl[A] Default for MapperResult[A] with default() { Default }

///|
pub(all) typealias FilterMap[A] = A?

///|
pub fn Mapper::delete[A]() -> MapperResult[A] {
  Map(None)
}

///|
pub fn Mapper::ret[A](a : A) -> MapperResult[A] {
  Map(Some(a))
}

///|
pub fn Mapper::none[A](self : Mapper, _a : A) -> MapperResult[A] {
  let _ = self
  Default
}

///|
pub fn Mapper::inline_ext_none[A](
  self : Mapper,
  _a : A
) -> FilterMap[A]!MapperError {
  let _ = self
  raise MapperError("unknown inline extension")
}

///|
pub fn Mapper::block_ext_none[A](
  self : Mapper,
  _a : A
) -> FilterMap[A]!MapperError {
  let _ = self
  raise MapperError("unknown block extension")
}

///|
pub fn Mapper::new(
  inline_ext_default~ : MapFn[Inline] = Mapper::inline_ext_none,
  block_ext_default~ : MapFn[Block] = Mapper::block_ext_none,
  inline~ : MapperFn[Inline] = Mapper::none,
  block~ : MapperFn[Block] = Mapper::none
) -> Mapper {
  { inline_ext_default, block_ext_default, inline, block }
}

///|
pub fn Mapper::map_inline(self : Mapper, i : Inline) -> FilterMap[Inline] {
  guard let Default = (self.inline)(self, i) else { Map(i) => i }
  match i {
    Autolink(_)
    | Break(_)
    | CodeSpan(_)
    | RawHtml(_)
    | Text(_)
    | ExtMathSpan(_) => Some(i)
    Image({ v, meta }) => {
      let text = self.map_inline(v.text).or(Inline::empty())
      Some(Image({ v: { ..v, text, }, meta }))
    }
    Link({ v, meta }) => {
      guard let Some(text) = self.map_inline(v.text) else { _ => None }
      Some(Link({ v: { ..v, text, }, meta }))
    }
    Emphasis({ v, meta }) => {
      guard let Some(inline) = self.map_inline(v.inline) else { _ => None }
      Some(Emphasis({ v: { ..v, inline, }, meta }))
    }
    StrongEmphasis({ v, meta }) => {
      guard let Some(inline) = self.map_inline(v.inline) else { _ => None }
      Some(StrongEmphasis({ v: { ..v, inline, }, meta }))
    }
    Inlines({ v, meta }) => {
      guard let Cons(_, _) as v = v.filter_map(fn(x) { self.map_inline(x) }) else {
        _ => None
      }
      Some(Inlines({ v, meta }))
    }
    ExtStrikethrough({ v, meta }) => {
      guard let Some(v) = self.map_inline(v._) else { _ => None }
      Some(ExtStrikethrough({ v, meta }))
    }
    // ext => (self.inline_ext_default)(self, ext)
  }
}

///|
pub fn Mapper::map_block(self : Mapper, b : Block) -> FilterMap[Block] {
  guard let Default = (self.block)(self, b) else { Map(b) => b }
  match b {
    BlankLine(_)
    | CodeBlock(_)
    | HtmlBlock(_)
    | LinkRefDefinition(_)
    | ThematicBreak(_)
    | ExtMathBlock(_) as b => Some(b)
    Heading({ v, meta }) => {
      let inline = self
        .map_inline(v.inline)
        .or(Inlines({ v: Nil, meta: Meta::none() }))
      Some(Heading({ v: { ..v, inline, }, meta }))
    }
    BlockQuote({ v, meta }) => {
      let block = self
        .map_block(v.block)
        .or(Blocks({ v: Nil, meta: Meta::none() }))
      Some(BlockQuote({ v: { ..v, block, }, meta }))
    }
    Blocks({ v, meta }) =>
      match v.filter_map(fn(x) { self.map_block(x) }) {
        Nil => None
        v => Some(Blocks({ v, meta }))
      }
    List({ v, meta }) => {
      fn map_list_item(i : Node[ListItem]) {
        let { v, meta } = i
        guard let Some(block) = self.map_block(i.v.block) else { _ => None }
        Some({ v: { ..v, block, }, meta })
      }

      match v.items.filter_map(map_list_item) {
        Nil => None
        items => Some(List({ v: { ..v, items, }, meta }))
      }
    }
    Paragraph({ v, meta }) => {
      guard let Some(inline) = self.map_inline(v.inline) else { _ => None }
      Some(Paragraph({ v: { ..v, inline, }, meta }))
    }
    ExtFootnoteDefinition({ v, meta }) => {
      let block = self
        .map_block(v.block)
        .or_else(fn() { Blocks(Node::new(Nil)) }) // Can be empty
      Some(ExtFootnoteDefinition({ v: { ..v, block, }, meta }))
    }
    ExtTable({ v, meta }) => {
      fn map_col(i, layout) {
        self.map_inline(i).map(fn(i) { (i, layout) })
      }

      fn map_row(row) {
        let ({ v, meta }, blanks) = row
        match v {
          Header(is_) => {
            let is_ = is_.filter_map(fn(i) { map_col(i.0, i.1) })
            ({ v: Header(is_), meta }, blanks)
          }
          Sep(_) => row
          Data(is_) => {
            let is_ = is_.filter_map(fn(i) { map_col(i.0, i.1) })
            ({ v: Data(is_), meta }, blanks)
          }
        }
      }

      Some(ExtTable({ v: { ..v, rows: v.rows.map(map_row) }, meta }))
    }
    // ext => (self.block_ext_default)(self, ext)
  }
}

///|
pub fn Mapper::map_doc(self : Mapper, d : Doc) -> Doc {
  let map_block = fn(b) { self.map_block(b).or(Block::empty()) }
  let map_def = fn(b : LabelDef) {
    guard let FootnoteDef({ v, meta }) = b else { b => b }
    let block = map_block(v.block)
    FootnoteDef({ v: { ..v, block, }, meta })
  }
  let block = map_block(d.block)
  let defs = d.defs.map(map_def)
  { ..d, block, defs }
}
</code></pre>
  <script>
    let moonbitLanguageFn = hljs => {
      return {
        case_insensitive: true,
        keywords: {
          keyword: 'func fn enum struct type if else match return continue break while let var interface pub priv readonly',
          literal: 'true false',
          type: "Int Int64 Double String Bool Char Bytes Option Array Result",
          built_in: 'lsl lsr asr shl shr land lor lxor Show Debug Hash Eq Compare Some None'
        },
        contains: [
          {
            scope: "char",
            begin: "'", end: "'"
          },
          {
            scope: "string",
            begin: "\"", end: "\""
          },
          {
            scope: "number",
            begin: "\\b\\d+(\\.\\d+)?\\b"
          },
          {
            scope: "codelink",
            match: /\<a href\="(?<link>[^<>]+?)"\>(?<code>[^\/<>]+?)\<\/a\>/g
          },
          hljs.COMMENT(
            '//', // begin
            '\n', // end
          )
        ]
      }
    }

    hljs.registerLanguage('moonbit', moonbitLanguageFn);
    hljs.highlightAll();
    hljs.initLineNumbersOnLoad();

    const number = window.location.href.split('#')[1];

    function waitForLineNumbers() {
      setTimeout(function () {
        const target = document.querySelector(`.hljs-ln-line[data-line-number="${number}"]`);
        if (target == null) waitForLineNumbers();
        else target.scrollIntoView();
      }, 50);
    }

    waitForLineNumbers()

  </script>
  <style>
    .hljs-ln-numbers {
      -webkit-touch-callout: none;
      -webkit-user-select: none;
      -khtml-user-select: none;
      -moz-user-select: none;
      -ms-user-select: none;
      user-select: none;
    }

    .hljs-ln-n {
      color: #ccc;
      border-right: 1px solid #dfdddd;
      margin-right: 1em;
      text-align: center;
      vertical-align: top;
      padding-right: 0.5em;
    }

    .hljs {
      background: none;
    }

    body {
      background-color: #fafafa;
    }
  </style>
</body>

</html>