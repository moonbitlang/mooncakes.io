<!DOCTYPE html>
<html lang="en">

<head>
  <meta charset="UTF-8">
  <title>Moonbit docs</title>
  <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
  <meta name="description" content="Description">
  <meta name="viewport" content="width=device-width, initial-scale=1.0, minimum-scale=1.0">
  <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/styles/base16/one-light.css">
  <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/highlight.min.js"></script>
  <script
    src="//cdnjs.cloudflare.com/ajax/libs/highlightjs-line-numbers.js/2.8.0/highlightjs-line-numbers.min.js"></script>
</head>

<body>
  <pre><code class="moonbit">///| `get_memory` returns the (unprocessed) host Memory block for the "var" data associated with
/// the provided `key`.
pub fn get_memory(key : String) -> @host.Memory? {
  let key_mem = @host.allocate_string(key)
  let offset = @extism.var_get(key_mem.offset)
  key_mem.free()
  if offset == 0L {
    return None
  }
  let length = @extism.length(offset)
  if length == 0L {
    return None
  }
  Some({ offset, length })
}

///| `get_bytes` returns the (unprocessed) host Memory block for the "var" data associated with
/// the provided `key`.
pub fn get_bytes(key : String) -> Bytes? {
  match get_memory(key) {
    Some(v) => Some(v.to_bytes())
    None => None
  }
}

///| `get_int` returns the host's "var" Int associated with the provided `key`.
pub fn get_int(key : String) -> Int? {
  match get_memory(key) {
    Some(v) => Some(v.to_int())
    None => None
  }
}

///| `get_string` returns the host's "var" String associated with the provided `key`.
/// Note that the Extism host string is UTF-8 and the string is converted to
/// a MoonBit (UTF-16) String.
pub fn get_string(key : String) -> String? {
  match get_memory(key) {
    Some(v) => Some(v.to_string())
    None => None
  }
}

///| `set_bytes` sets the (unprocessed) host Memory block for the "var" data associated with
/// the provided `key`.
pub fn set_bytes(key : String, value : Bytes) -> Unit {
  let key_mem = @host.allocate_string(key)
  let val_mem = @host.allocate_bytes(value)
  @extism.var_set(key_mem.offset, val_mem.offset)
  key_mem.free()
  val_mem.free()
}

///| `set_int` sets the host's "var" Int associated with the provided `key`.
pub fn set_int(key : String, value : Int) -> Unit {
  let key_mem = @host.allocate_string(key)
  let bytes = Bytes::new(4)
  bytes[0] = value.land(255).to_byte()
  bytes[1] = (value >> 8).land(255).to_byte()
  bytes[2] = (value >> 16).land(255).to_byte()
  bytes[3] = (value >> 24).land(255).to_byte()
  let val_mem = @host.allocate_bytes(bytes)
  @extism.var_set(key_mem.offset, val_mem.offset)
  key_mem.free()
  val_mem.free()
}

///| `set_string` sets the host Memory block for the "var" data associated with
/// the provided `key`.
/// Note that the MoonBit String is UTF-16 and the string is converted to
/// a UTF-8 string for the Extism host.
pub fn set_string(key : String, value : String) -> Unit {
  let key_mem = @host.allocate_string(key)
  let val_mem = @host.allocate_string(value)
  @extism.var_set(key_mem.offset, val_mem.offset)
  key_mem.free()
  val_mem.free()
}

///| `remove` deletes the value in the host's "var" memory associated with the provided `key`.
pub fn remove(key : String) -> Unit {
  let key_mem = @host.allocate_string(key)
  @extism.var_set(key_mem.offset, 0L)
  key_mem.free()
}
</code></pre>
  <script>
    let moonbitLanguageFn = hljs => {
      return {
        case_insensitive: true,
        keywords: {
          keyword: 'func fn enum struct type if else match return continue break while let var interface pub priv readonly',
          literal: 'true false',
          type: "Int Int64 Double String Bool Char Bytes Option Array Result",
          built_in: 'lsl lsr asr shl shr land lor lxor Show Debug Hash Eq Compare Some None'
        },
        contains: [
          {
            scope: "char",
            begin: "'", end: "'"
          },
          {
            scope: "string",
            begin: "\"", end: "\""
          },
          {
            scope: "number",
            begin: "\\b\\d+(\\.\\d+)?\\b"
          },
          {
            scope: "codelink",
            match: /\<a href\="(?<link>[^<>]+?)"\>(?<code>[^\/<>]+?)\<\/a\>/g
          },
          hljs.COMMENT(
            '//', // begin
            '\n', // end
          )
        ]
      }
    }

    hljs.registerLanguage('moonbit', moonbitLanguageFn);
    hljs.highlightAll();
    hljs.initLineNumbersOnLoad();

    const number = window.location.href.split('#')[1];

    function waitForLineNumbers() {
      setTimeout(function () {
        const target = document.querySelector(`.hljs-ln-line[data-line-number="${number}"]`);
        if (target == null) waitForLineNumbers();
        else target.scrollIntoView();
      }, 50);
    }

    waitForLineNumbers()

  </script>
  <style>
    .hljs-ln-numbers {
      -webkit-touch-callout: none;
      -webkit-user-select: none;
      -khtml-user-select: none;
      -moz-user-select: none;
      -ms-user-select: none;
      user-select: none;
    }

    .hljs-ln-n {
      color: #ccc;
      border-right: 1px solid #dfdddd;
      margin-right: 1em;
      text-align: center;
      vertical-align: top;
      padding-right: 0.5em;
    }

    .hljs {
      background: none;
    }

    body {
      background-color: #fafafa;
    }
  </style>
</body>

</html>