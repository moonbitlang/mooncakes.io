<!DOCTYPE html>
<html lang="en">

<head>
  <meta charset="UTF-8">
  <title>Moonbit docs</title>
  <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
  <meta name="description" content="Description">
  <meta name="viewport" content="width=device-width, initial-scale=1.0, minimum-scale=1.0">
  <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/styles/base16/one-light.css">
  <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/highlight.min.js"></script>
  <script
    src="//cdnjs.cloudflare.com/ajax/libs/highlightjs-line-numbers.js/2.8.0/highlightjs-line-numbers.min.js"></script>
</head>

<body>
  <pre><code class="moonbit">///|
enum WriterNode {
  Nil
  Leaf
  SimpleExpr
  BoxedExpr
  Expr
} derive(Eq)

///|
let char_close : Char = ')'

///|
let char_open : Char = '('

///|
let allowed_chars : String = "$-:<>[]{}*=+.,\\/!?~_@#&%^|;'"

///|
fn is_a_digit(c : Char) -> Bool {
  let n = c.to_int()
  // ascii table https://tool.oschina.net/commons?type=4
  n >= 48 && n <= 57
}

///|
fn is_a_letter(c : Char) -> Bool {
  let n = c.to_int()
  if n >= 65 && n <= 90 {
    return true
  }
  if n >= 97 && n <= 122 {
    return true
  }
  false
}

///|
fn is_simple_expr(ys : Array[Cirru]) -> Bool {
  for y in ys {
    match y {
      Cirru::List(_) => return false
      Cirru::Leaf(_) => ()
    }
  }
  true
}

///|
fn is_boxed(ys : Array[Cirru]) -> Bool {
  for y in ys {
    match y {
      Cirru::List(_) => return false
      Cirru::Leaf(_) => ()
    }
  }
  true
}

///|
fn is_simple_char(x : Char) -> Bool {
  is_a_letter(x) || is_a_digit(x)
}

///|
fn is_char_allowed(x : Char) -> Bool {
  if is_simple_char(x) {
    return true
  }
  allowed_chars.contains_char(x)
}

///|
fn generate_leaf(s : String) -> String {
  let mut all_allowed = true
  for x in s {
    if not(is_char_allowed(x)) {
      all_allowed = false
      break
    }
  }
  if all_allowed {
    s.to_string()
  } else {
    let mut ret = ""
    ret = ret + "\""
    for c in s {
      ret = ret +
        (match c {
          '\n' => "\\n"
          '\t' => "\\t"
          '\"' => "\\\""
          '\\' => "\\\\"
          '\'' => "\\'"
          _ => "\{c}"
        })
    }
    ret = ret + "\""
    ret
  }
}

///|
fn generate_empty_expr() -> String {
  "()"
}

///|
fn generate_inline_expr(xs : Array[Cirru]) -> String {
  let mut result = char_open.to_string()
  for idx, x in xs {
    if idx > 0 {
      result = result + " "
    }
    let piece = match x {
      Cirru::Leaf(s) => generate_leaf(s)
      Cirru::List(ys) => generate_inline_expr(ys)
    }
    result = result + piece
  }
  result = result + char_close.to_string()
  result
}

///| by 2 spaces
fn push_spaces(buf : String, n : Int) -> String {
  let mut ret = buf
  // for _ in 0..n {
  //   buf.push_str("  ");
  // }
  for i = 0; i < n; i = i + 1 {
    ret = ret + "  "
  }
  return ret
}

///|
fn render_newline(n : Int) -> String {
  let mut ret = ""
  ret = ret + "\n"
  ret = push_spaces(ret, n)
  ret
}

///| options for writer, `use_inline` for more compact format.
pub(all) struct CirruWriterOptions {
  use_inline : Bool
}

///| kind for writer nodes
fn get_node_kind(self : Cirru) -> WriterNode {
  match self {
    Cirru::Leaf(_) => WriterNode::Leaf
    Cirru::List(xs) =>
      if xs.is_empty() {
        WriterNode::Leaf
      } else if is_simple_expr(xs) {
        WriterNode::SimpleExpr
      } else if is_boxed(xs) {
        WriterNode::BoxedExpr
      } else {
        WriterNode::Expr
      }
  }
}

///|
pub(all) type! FormatCirruError String

///|
pub fn to_string(self : FormatCirruError) -> String {
  match self {
    FormatCirruError(s) => s
  }
}

///|
fn generate_tree(
  xs : Array[Cirru],
  insist_head : Bool,
  options : CirruWriterOptions,
  base_level : Int,
  in_tail : Bool
) -> String!FormatCirruError {
  let mut prev_kind = WriterNode::Nil
  let mut level = base_level
  let mut result = ""
  for idx, cursor in xs {
    let kind = get_node_kind(cursor)
    let next_level = level + 1
    let child_insist_head = prev_kind == WriterNode::BoxedExpr ||
      prev_kind == WriterNode::Expr
    let at_tail = idx != 0 &&
      not(in_tail) &&
      prev_kind == WriterNode::Leaf &&
      idx == xs.length() - 1

    // println!("\nloop {:?} {:?}", prev_kind, kind);
    // println!("cursor {:?} {} {}", cursor, idx, insist_head);
    // println!("{:?}", result);

    let child : String = match cursor {
      Cirru::Leaf(s) => generate_leaf(s)
      Cirru::List(ys) =>
        if at_tail {
          if ys.is_empty() {
            "$"
          } else {
            let mut ret = "$ "
            ret = ret + generate_tree!(ys, false, options, level, at_tail)
            ret
          }
        } else if idx == 0 && insist_head {
          generate_inline_expr(ys)
        } else if kind == WriterNode::Leaf {
          if idx == 0 {
            let mut ret = render_newline(level)
            ret = ret + generate_empty_expr()
            ret
          } else {
            generate_empty_expr() // special since empty expr is treated as leaf
          }
        } else if kind == WriterNode::SimpleExpr {
          if prev_kind == WriterNode::Leaf {
            generate_inline_expr(ys)
          } else if options.use_inline && prev_kind == WriterNode::SimpleExpr {
            let mut ret = " "
            ret = ret + generate_inline_expr(ys)
            ret
          } else {
            let mut ret = render_newline(next_level)
            ret = ret +
              generate_tree!(ys, child_insist_head, options, next_level, false)
            ret
          }
        } else if kind == WriterNode::Expr {
          let content = generate_tree!(
            ys, child_insist_head, options, next_level, false,
          )
          if content.starts_with("\n") {
            content
          } else {
            let mut ret = render_newline(next_level)
            ret = ret + content
            ret
          }
        } else if kind == WriterNode::BoxedExpr {
          let content = generate_tree!(
            ys, child_insist_head, options, next_level, false,
          )
          if prev_kind == WriterNode::Nil ||
            prev_kind == WriterNode::Leaf ||
            prev_kind == WriterNode::SimpleExpr {
            content
          } else {
            let mut ret = render_newline(next_level)
            ret = ret + content
            ret
          }
        } else {
          raise FormatCirruError("Unpected condition")
        }
    }
    let bended = kind == WriterNode::Leaf &&
      (prev_kind == WriterNode::BoxedExpr || prev_kind == WriterNode::Expr)
    let chunk = if at_tail ||
      (prev_kind == WriterNode::Leaf && kind == WriterNode::Leaf) ||
      (prev_kind == WriterNode::Leaf && kind == WriterNode::SimpleExpr) ||
      (prev_kind == WriterNode::SimpleExpr && kind == WriterNode::Leaf) {
      let mut ret = " "
      ret = ret + child
      ret
    } else if bended {
      let mut ret = render_newline(next_level)
      ret = ret + ", " + child
      ret
    } else {
      child
    }
    result = result + chunk

    // update writer states

    if kind == WriterNode::SimpleExpr {
      if idx == 0 && insist_head {
        prev_kind = WriterNode::SimpleExpr
      } else if options.use_inline {
        if prev_kind == WriterNode::Leaf || prev_kind == WriterNode::SimpleExpr {
          prev_kind = WriterNode::SimpleExpr
        } else {
          prev_kind = WriterNode::Expr
        }
      } else if prev_kind == WriterNode::Leaf {
        prev_kind = WriterNode::SimpleExpr
      } else {
        prev_kind = WriterNode::Expr
      }
    } else {
      prev_kind = kind
    }
    if bended {
      level += 1
    }
  }

  // console.log("chunk", JSON.stringify(chunk));
  // console.log("And result", JSON.stringify(result));
  result
}

///|
fn generate_statements(
  ys : Array[Cirru],
  options : CirruWriterOptions
) -> String!FormatCirruError {
  let mut zs = ""
  for y in ys {
    match y {
      Cirru::Leaf(_) => raise FormatCirruError("expected an exprs at top level")
      Cirru::List(cs) => {
        zs = zs + "\n"
        zs = zs + generate_tree!(cs, true, options, 0, false)
        zs = zs + "\n"
      }
    }
  }
  zs
}

///| format Cirru code, use options to control `use_inline` option
pub fn format(
  xs : Array[Cirru],
  options : CirruWriterOptions
) -> String!FormatCirruError {
  generate_statements!(xs, options)
}
</code></pre>
  <script>
    let moonbitLanguageFn = hljs => {
      return {
        case_insensitive: true,
        keywords: {
          keyword: 'func fn enum struct type if else match return continue break while let var interface pub priv readonly',
          literal: 'true false',
          type: "Int Int64 Double String Bool Char Bytes Option Array Result",
          built_in: 'lsl lsr asr shl shr land lor lxor Show Debug Hash Eq Compare Some None'
        },
        contains: [
          {
            scope: "char",
            begin: "'", end: "'"
          },
          {
            scope: "string",
            begin: "\"", end: "\""
          },
          {
            scope: "number",
            begin: "\\b\\d+(\\.\\d+)?\\b"
          },
          {
            scope: "codelink",
            match: /\<a href\="(?<link>[^<>]+?)"\>(?<code>[^\/<>]+?)\<\/a\>/g
          },
          hljs.COMMENT(
            '//', // begin
            '\n', // end
          )
        ]
      }
    }

    hljs.registerLanguage('moonbit', moonbitLanguageFn);
    hljs.highlightAll();
    hljs.initLineNumbersOnLoad();

    const number = window.location.href.split('#')[1];

    function waitForLineNumbers() {
      setTimeout(function () {
        const target = document.querySelector(`.hljs-ln-line[data-line-number="${number}"]`);
        if (target == null) waitForLineNumbers();
        else target.scrollIntoView();
      }, 50);
    }

    waitForLineNumbers()

  </script>
  <style>
    .hljs-ln-numbers {
      -webkit-touch-callout: none;
      -webkit-user-select: none;
      -khtml-user-select: none;
      -moz-user-select: none;
      -ms-user-select: none;
      user-select: none;
    }

    .hljs-ln-n {
      color: #ccc;
      border-right: 1px solid #dfdddd;
      margin-right: 1em;
      text-align: center;
      vertical-align: top;
      padding-right: 0.5em;
    }

    .hljs {
      background: none;
    }

    body {
      background-color: #fafafa;
    }
  </style>
</body>

</html>